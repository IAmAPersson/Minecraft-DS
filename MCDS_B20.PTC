'–Minecraft DS made by the–
'–PTC Community. The code –
'–is really complex, so I –
'– recommend not messing  –
'–with it unless you know –
'– what you're doing (or  –
'–you REALLY want to have –
'– fun). I hope you enjoy –
'–  playing Minecraft DS, –
'–  and please watch the  –
'–        credits!        –
'–                        –
'–  -Phil (IAmAPersson)   –
'š••••••••••••••••••••••••›
'˜••••••••••••••••••••••••™
'–Version Beta 2.0.0 Pre 2–
'š••••••••••••••••••••••••›

@HHS
ACLS
BGMSTOP
PNLTYPE "OFF"
INTXT1$="Minecraft DS is in no way"
INTXT2$="associated with Mojang AB"
INTXT3$="and does not own the name"
INTXT4$="        Minecraft.       "
FOR I=0TO 440STEP 5
IF PAGE==3THEN PAGE=2:GPAGE 0,3,2 ELSE PAGE=3:GPAGE 0,2,3
VSYNC 2
GCLS 
FOR J=0TO LEN(INTXT1$)-1
GPUTCHR -187+I+J*8,80,"BGF",ASC(MID$(INTXT1$,J,1)),0,1
GPUTCHR 243-I+J*8,88,"BGF",ASC(MID$(INTXT2$,J,1)),0,1
GPUTCHR -187+I+J*8,96,"BGF",ASC(MID$(INTXT3$,J,1)),0,1
GPUTCHR 243-I+J*8,104,"BGF",ASC(MID$(INTXT4$,J,1)),0,1
NEXT
IF I==220THEN WAIT 150
NEXT
ACLS
INTXT21$="Made By The PTC Community"
FOR I=0TO 220STEP 5
IF PAGE==3THEN PAGE=2:GPAGE 0,3,2 ELSE PAGE=3:GPAGE 0,2,3
VSYNC 2
GCLS
FOR J=1TO LEN(INTXT21$)-1STEP 2
GPUTCHR 243-I+J*8-4,88,"BGF",ASC(MID$(INTXT21$,J,1)),0,1
GPUTCHR -187+I+J*8-12,88,"BGF",ASC(MID$(INTXT21$,J-1,1)),0,1
NEXT
NEXT
LOCATE 3,11:PRINT INTXT21$
LOCATE 11,12:?"Loading..."
@A
VISIBLE 1,0,0,0,0,0
CLEAR
TPCHN$="MCDS
HAND1$="MCHAND1
HAND2$="MCHAND2
HANDY=13
GPAGE 0,1,1
SYSBEEP=0
CHRINIT "BGF0"
SYSBEEP=1
BGMSETD 128,@WETHANDS
MS=16
CREDITLINES=34
DIM TGEN(5)
DIM SPLASH$(59)
DIM MAP(128,128)
DIM MAPB(128,128)
DIM NETHER(128,128)
DIM NETHERB(128,128)
DIM TEX(1000,16)
DIM BLOCK1(16,16)
DIM BLOCK2(16,16)
DIM BLOCK3(16,16)
DIM BLOCK4(16,16)
DIM BLOCK5(16,16)
DIM BLOCK6(16,16)
DIM B(6)
DIM CREDITS$(CREDITLINES)
DIM BREAKABLE(40)
DIM SCREENSHOT(256,192)
DIM MOB_X(100)
DIM MOB_Y(100)
DIM MOB_LAYER(100)
DIM MOB_DIRECTION(100)
DIM CRP(5,10)
DIM M(64,64)
DIM T(512,16)
TPCHN$="GRP:MCDS"
HAND1$="SPU0:"+HAND1$
HAND2$="BGU0:"+HAND2$
@DOOPY
SYSBEEP=0
LOAD HAND1$,0
LOAD HAND2$,0
SYSBEEP=1
MENU=1
CA=0
CX=0
CY=0
STOREDX=5
STOREDY=3
MST=.2
MOB_COUNT=5
MOB_ENABLED=1
MOD_ENABLED=0
VERS$="Beta 2.0.0"
SSS$="On"
MSS$="Off"
RSS$="On"
TSS$="On"
RDIST$="16"
RDIST=VAL(RDIST$)
BOB$="On"
B1$="@118B18"
SWV=120
GOSUB@PROGRAM
GOSUB@INITGFX
IF PAGE==3THEN PAGE=2:GPAGE 0,3,2 ELSE PAGE=3:GPAGE 0,2,3
BGMPLAY 128
KEY 1,"/quality"
KEY 2,"/help"
KEY 3,"/nick"
KEY 4,"/data"
KEY 5,"/me"
GOSUB@MENU
GOSUB@TEXT

@PROGRAM
PNLTYPE"OFF"
TDS=1
O=4095/4096
GPAGE 0,0,2
SYSBEEP=0
LOAD TPCHN$,0
SYSBEEP=1
FOR I=0TO 255
FOR J=0TO 15
TEX(I,J)=GSPOIT(I,J)
NEXT
NEXT
FOR I=0TO 255
FOR J=0TO 15
TEX(I+256,J)=GSPOIT(I,J+16)
NEXT
NEXT
FOR I=0TO 255
FOR J=0TO 15
TEX(I+512,J)=GSPOIT(I,J+32)
NEXT
NEXT
RESTORE @BLOCKBREAK
FOR I=0TO 35
READ BREAKABLE(I)
NEXT
GPAGE 0,0,0
GCLS
BLOCK=1
'X COORDS
CAMX=6
'Y COORDS
CAMY=14.5
'Z COORDS
CAMZ=0.5
'LEFT AND RIGHT
CAMA=-RND(10)
'UP AND DOWN
CAMB=0
'QUALITY
Q=2
TOUCHING=0
JUMPING=0
FOR I=0TO 5
B(I)=I+1
NEXT
@INVBSU
FOR I=0TO 15
FOR J=0TO 15
BLOCK1(I,J)=TEX(I+(16*B(0))-16,J)
BLOCK2(I,J)=TEX(I+(16*B(1))-16,J)
BLOCK3(I,J)=TEX(I+(16*B(2))-16,J)
BLOCK4(I,J)=TEX(I+(16*B(3))-16,J)
BLOCK5(I,J)=TEX(I+(16*B(4))-16,J)
BLOCK6(I,J)=TEX(I+(16*B(5))-16,J)
NEXT
NEXT
IF INVST THEN INVST=0:RETURN
ST=1:GOSUB@LOADMAP:ST=0
RETURN

@LOOP1
IF !MENU THEN GOSUB@TGUIS
SYSBEEP=0
LOAD"GRP0:MCDS_BG",0
IF INNETHER THEN LOAD"GRP:NETHER",0
SYSBEEP=1
IF !MENU THEN BGMSTOP
DAY=MAINCNTL

@MAINLOOP
IF TSS$=="Off"OR INNETHER==1THEN GOTO@SKIPTIME
IF MAINCNTL>=DAY+3600AND DLP==0THEN GPAGE 0,0,0:SYSBEEP=0:LOAD"GRP:MCDS_NBG",0:SYSBEEP=1:DLP=1
IF MAINCNTL>=DAY+7200THEN GPAGE 0,0,0:SYSBEEP=0:LOAD"GRP:MCDS_BG",0:DAY=MAINCNTL:SYSBEEP=1:DLP=0
@SKIPTIME
IF PAGE==3THEN PAGE=2:GPAGE 0,3,2 ELSE PAGE=3:GPAGE 0,2,3
IF PAGE==2AND MOB_ENABLED THEN PTDC=PTDC+1
IF PTDC==2THEN PTDC=0:GOSUB@SLIME
GOSUB@MOVE
GCOPY 0,0,0,255,191,0,0,FALSE
IF RSS$=="On"THEN GOSUB@DRAWGROUND ELSE GFILL 0,97-CAMB,255,191,26*!INNETHER+211*INNETHER
GOSUB@DRAWWALLS
CAMZ=CAMZ-1
GOSUB@DRAWWALLS
GFILL 0,0,ABS(SWV-128),191,0
GFILL 255-ABS(SWV-128),0,255,191,0
IF MENU THEN RETURN
IF MOD_ENABLED THEN GOSUB@MODS
IF TCHST THEN GOSUB@TGUI
GOSUB@FIST
FPS=FPS+1
IF NEXTCOUNT>=MAINCNTL GOTO@NOFPS
FPS=FPS-(MAINCNTL-NEXTCOUNT)/60
NEXTCOUNT=MAINCNTL+60
COLOR 13:LOCATE 0,22:?FPS" FPS         ":COLOR 0
FPS=0
@NOFPS
IF VT!=1THEN VISIBLE 1,1,1,1,1,1 ELSE VT=1
GOTO@MAINLOOP

@FIST
SPSET 0,0,HANDY,0,0,0,64,64
SPHOME 0,-128,-128
BGFILL 0,13,17,15,22,0,13,0,0
RETURN

@MOVE
IF BNCF THEN BNCF=0:MAPB(BSI,BSJ)=BLOCK:MAP(BSI,BSJ)=0
IF !MENU AND BUTTON()==768 THEN GOSUB @SCREENSHOT:COLOR 0:ACLS
IF TCHST AND TCHX>=206AND TCHY>=140AND TCHX<=252AND TCHY<=154THEN GOSUB@BLOCKS
IF TCHST AND TCHX>=150AND TCHY>=140AND TCHX<=197AND TCHY<=154THEN GOSUB@BLOCKS
X=TCHX
Y=TCHY
B=TCHST
CAMAT=0
IF !MENU THEN IF BUTTON()AND 4THEN CAMAT=CAMAT+0.3
IF !MENU THEN IF BUTTON()AND 8THEN CAMAT=CAMAT-0.3
IF MENU THEN CAMAT=CAMAT-0.02
CAMA=CAMA+CAMAT
IF !MENU AND B AND X>=91AND Y>=117AND X<140AND Y<130THEN CAMB=CAMB-20
IF !MENU AND B AND X>=91AND Y>=140AND X<142AND Y<156THEN CAMB=CAMB+20
IF !MENU AND B AND X>=150AND Y>=117AND X<197AND Y<132THEN CAMA=CAMA+.3
IF !MENU AND B AND X>=206AND Y>=117AND X<250AND Y<132THEN CAMA=CAMA-.3
IF CAMA>-7.8THEN CAMA=-14
IF CAMA<-14.197THEN CAMA=-8
IF ABS(CAMB)>150 THEN CAMB=150*SGN(CAMB)
@NOTOUCH
TOUCHING=B
TOUCHX=X
TOUCHY=Y

DX=COS(CAMA)*.4
DY=SIN(CAMA)*.4
X=CAMX
Y=CAMY
B=BUTTON()
IF !JUMPING GOTO@NOTJUMPING
CAMZ=CAMZ+VELZ
VELZ=VELZ-.06
IF CAMZ<.85THEN CAMZ=.85:JUMPING=0
GOTO@JUMPEND
@NOTJUMPING
IF BOB$=="On"THEN WALKING=WALKING+0.9
IF BOB$=="On"THEN IF !(B AND 1)AND !(B AND 16)AND !(B AND 128)AND !(B AND 2)THEN WALKING=0
CAMZ=1.7+ABS(SIN(WALKING))*.15

@JUMPEND
IF !MENU THEN IF B AND 1THEN X=X+DX:Y=Y+DY
IF !MENU THEN IF B AND 2THEN X=X-DX:Y=Y-DY
IF !MENU THEN IF B AND 128THEN X=X-DY:Y=Y+DX
IF !MENU THEN IF B AND 16THEN X=X+DY:Y=Y-DX
IF !MENU THEN IF B==65THEN X=X+(DX*1.5):Y=Y+(DY*1.5)
IF !MENU THEN IF B AND 1024 THEN @PAUSE
IF TCHST AND TCHX>28 AND TCHY>92AND TCHX<42AND TCHY<108THEN X=X+DX:Y=Y+DY
IF TCHST AND TCHX>28AND TCHY>142AND TCHX<42AND TCHY<157THEN X=X-DX:Y=Y-DY
IF TCHST AND TCHX>6AND TCHY>116AND TCHX<19AND TCHY<132THEN X=X-DY:Y=Y+DX
IF TCHST AND TCHX>50AND TCHY>116AND TCHX<66AND TCHY<132THEN X=X+DY:Y=Y-DX
IF TCHST AND TCHX>114AND TCHY>88AND TCHX<148AND TCHY<109THEN X=X+(DX*1.5):Y=Y+(DY*1.5)
IF BUTTON()AND 256THEN GOSUB@BLOCKS
IF BUTTON()AND 512THEN GOSUB@BLOCKS
IF !MENU AND TCHST AND TCHX>210AND TCHY>165THEN GOSUB@S2
IF MAP(X,Y)==18THEN GOSUB@NETHERLOAD
IF GM!=2THEN IF MAP(X,Y)OR MAPB(X,Y)THEN RETURN
'BEHOLD! The line that crashes PTC!
IF GM==2THEN IF MAP(X,Y)OR MAPB(X,Y)THEN ?SIN(FLOOR(-524287.5))
CAMX=X
CAMY=Y
CA=CAMA+6.2
CX=CAMX/6.2
CY=CAMY/6.2
RETURN

@TGUI
TGUIDR=0
TX=TCHX
TY=TCHY
BO=BLOCK
IF TCHST AND TX<63AND TY>174GOTO@INVENTORY
IF TCHST AND TX>63AND TY>174AND TX<82AND TY<190THEN BLOCK=B(0):IIH=1
IF TCHST AND TX>87AND TY>174AND TX<106AND TY<190THEN BLOCK=B(1):IIH=2
IF TCHST AND TX>111AND TY>174AND TX<130AND TY<190THEN BLOCK=B(2):IIH=3
IF TCHST AND TX>135AND TY>174AND TX<154AND TY<190THEN BLOCK=B(3):IIH=4
IF TCHST AND TX>159AND TY>174AND TX<177AND TY<190THEN BLOCK=B(4):IIH=5
IF TCHST AND TX>183AND TY>174AND TX<202AND TY<190THEN BLOCK=B(5):IIH=6
IF BO!=BLOCK THEN BEEP 9
IF BO!=BLOCK THEN PNLSTR 7,21,"‘••‘••‘••‘••‘••‘••‘"
COLOR 4
PNLSTR 5+(IIH*3),21,"••"
COLOR 15
RETURN

@TGUIS
IF !IIH THEN IIH=1
PNLSTR 7,21,"‘••‘••‘••‘••‘••‘••"
PNLSTR 7,22,"–  –  –  –  –  –  "
PNLSTR 7,23,"–  –  –  –  –  –  "
GPAGE 1,1,1
GOSUB@DIRTBG
GFILL 0,172,256,192,31
FOR TDX=0TO 15
FOR TDY=0TO 15
GPSET TDX+64,TDY+175,BLOCK1(TDX,TDY)
GPSET TDX+88,TDY+175,BLOCK2(TDX,TDY)
GPSET TDX+112,TDY+175,BLOCK3(TDX,TDY)
GPSET TDX+136,TDY+175,BLOCK4(TDX,TDY)
GPSET TDX+160,TDY+175,BLOCK5(TDX,TDY)
GPSET TDX+184,TDY+175,BLOCK6(TDX,TDY)
NEXT
NEXT
PNLSTR 25,21,"‘••••••"
PNLSTR 25,22,"–      "
PNLSTR 25,23,"–      "
PNLSTR 0,21,"•••••••‘"
PNLSTR 0,22,"       –"
PNLSTR 0,23,"       –"
GPUTCHR 214,180,"BGF",ASC("C"),0,1:GPUTCHR 222,180,"BGF",ASC("h"),0,1
GPUTCHR 230,180,"BGF",ASC("a"),0,1:GPUTCHR 237,180,"BGF",ASC("t"),0,1
GPUTCHR 2,180,"BGF",ASC("I"),0,1:GPUTCHR 8,180,"BGF",ASC("n"),0,1
GPUTCHR 14,180,"BGF",ASC("v"),0,1:GPUTCHR 20,180,"BGF",ASC("e"),0,1
GPUTCHR 26,180,"BGF",ASC("n"),0,1:GPUTCHR 32,180,"BGF",ASC("t"),0,1
GPUTCHR 38,180,"BGF",ASC("o"),0,1:GPUTCHR 44,180,"BGF",ASC("r"),0,1
GPUTCHR 50,180,"BGF",ASC("y"),0,1
PNLSTR 0,12,"        "
PNLSTR 0,15,"       "
PNLSTR 0,18,"        "
PNLSTR 21,12," Use  Dash "
PNLSTR 11,15," Look  Look  Look "
PNLSTR 11,18," Look  Break  Place "
BX1=25:BY1=90:BX2=45:BY2=105:GOSUB@BTN
BX1=25:BY1=140:BX2=45:BY2=155:GOSUB@BTN
BX1=50:BY1=115:BX2=70:BY2=130:GOSUB@BTN
BX1=2:BY1=115:BX2=20:BY2=130:GOSUB@BTN
BX1=171:BY1=90:BX2=205:BY2=108:GOSUB@BTN
BX1=210:BY1=90:BX2=252:BY2=108:GOSUB@BTN
BX1=90:BY1=115:BX2=140:BY2=133:GOSUB@BTN
BX1=147:BY1=115:BX2=195:BY2=133:GOSUB@BTN
BX1=205:BY1=115:BX2=252:BY2=133:GOSUB@BTN
BX1=90:BY1=140:BX2=140:BY2=158:GOSUB@BTN
BX1=147:BY1=140:BX2=195:BY2=158:GOSUB@BTN
BX1=205:BY1=140:BX2=252:BY2=158:GOSUB@BTN
GPAGE 0,0,0
LOCATE 16,12:COLOR 1:?"+":COLOR 0
GPAGE 0,2,3
RETURN

@DRAWWALLS
ANGSTEP=PI()*Q/3/256
CURRANG=PI()/6
DCD=0
FOR I=ABS(SWV-128)TO SWV+128STEP Q
C=COS(CAMA+CURRANG)
S=SIN(CAMA+CURRANG)
GOSUB@RAYCAST
DX=COLLX-CAMX
DY=COLLY-CAMY
IF ABS(DX)>50THEN DX=-10
DIST=SQR(DX*DX+DY*DY)
IF !DIST THEN DIST=1/4096
IF DCD THEN DCD=DCD-1:GOTO@DWRL
IF DIST>RDIST THEN DCD=1:GOTO@DWRL
H=12/DIST/COS(CURRANG)
T=(T+SLICE)*16
J=I+Q-1
IF H>13000THEN H=13000
P=96-CAMB+H*16*CAMZ-H
GFILL I,P,J,P+H,TEX(T,15)
GFILL I,P-H,J,P,TEX(T,14)
P=P-H*2
GFILL I,P,J,P+H,TEX(T,13)
GFILL I,P-H,J,P,TEX(T,12)
P=P-H*2
GFILL I,P,J,P+H,TEX(T,11)
GFILL I,P-H,J,P,TEX(T,10)
P=P-H*2
GFILL I,P,J,P+H,TEX(T,9)
GFILL I,P-H,J,P,TEX(T,8)
P=P-H*2
GFILL I,P,J,P+H,TEX(T,7)
GFILL I,P-H,J,P,TEX(T,6)
P=P-H*2
GFILL I,P,J,P+H,TEX(T,5)
GFILL I,P-H,J,P,TEX(T,4)
P=P-H*2
GFILL I,P,J,P+H,TEX(T,3)
GFILL I,P-H,J,P,TEX(T,2)
P=P-H*2
GFILL I,P,J,P+H,TEX(T,1)
GFILL I,P-H,J,P,TEX(T,0)
@DWRL
CURRANG=CURRANG-ANGSTEP
NEXT
DCD=0
RETURN

@RAYCAST
IF ABS(C)>ABS(S)GOTO@CASTX
DY=SGN(S)
DX=C/ABS(S)
Y=FLOOR(CAMY)+O*(S<0)
X=CAMX-ABS(Y-CAMY)*DX

@CASTYLOOP
X=X+DX
Y=Y+DY
IF CAMZ<1AND !MAP(X,Y)AND !MAP(X,Y-DY)GOTO@CASTYLOOP
IF CAMZ>1AND !MAPB(X,Y)AND !MAPB(X,Y-DY)GOTO@CASTYLOOP
IF CAMZ<1THEN T=MAP(X,Y-DY)-1ELSE T=MAPB(X,Y-DY)-1
IF T<0GOTO@CASTYCOLL
COLLX=FLOOR(X)+O*(C<0)
COLLY=Y-(X-COLLX)/DX*DY
SLICE=ABS(COLLY%1-O*(C>0))
RETURN
@CASTYCOLL
COLLX=X
COLLY=Y
IF CAMZ<1THEN T=MAP(X,Y)-1ELSE T=MAPB(X,Y)-1
SLICE=ABS(COLLX%1-O*(S<0))
RETURN

@CASTX
DX=SGN(C)
DY=S/ABS(C)
X=FLOOR(CAMX)+O*(C<0)
Y=CAMY-ABS(X-CAMX)*DY

@CASTXLOOP
Y=Y+DY
X=X+DX
IF CAMZ<1AND !MAP(X,Y)AND !MAP(X-DX,Y)GOTO@CASTXLOOP
IF CAMZ>1AND !MAPB(X,Y)AND !MAPB(X-DX,Y)GOTO@CASTXLOOP
IF CAMZ<1THEN T=MAP(X-DX,Y)-1ELSE T=MAPB(X-DX,Y)-1
IF T<0GOTO@CASTXCOLL
COLLY=FLOOR(Y)+O*(S<0)
IF DY*DX==0THEN DY=.1:DX=.1
COLLX=X-(Y-COLLY)/DY*DX
SLICE=ABS(COLLX%1-O*(S<0))
RETURN
@CASTXCOLL
COLLX=X
COLLY=Y
IF CAMZ<1THEN T=MAP(X,Y)-1ELSE T=MAPB(X,Y)-1
SLICE=ABS(COLLY%1-O*(C>0))
RETURN

@SEEDINPUT
IF LANG==0THEN LOCATE 0,0:?"Please input a Seed"
IF LANG==1THEN LOCATE 0,0:?"Bitte schreiben Saatgut"
LOCATE 0,1:?""
LINPUT SEED$
LOCATE 0,0:?"                                "
LOCATE 0,1:?"                                "
PNLSTR 7,11,SEED$+"              ",0
RETURN

@NAMEINPUT
IF LANG==0THEN LOCATE 0,0:?"Please input the name you prefer"
IF LANG==1THEN LOCATE 0,0:?"Bitte schreiben Name Sie wollen"
?""
LINPUT NAME$
LOCATE 0,0:?"                                "
LOCATE 0,1:?"                                "
PNLSTR 7,5,NAME$+"              ",0
PNLSTR 6,7,"Will be saved in:              ",2
PNLSTR 6,7,"Will be saved in:"+NAME$,2
RETURN

@LOADMAP
IF ST GOTO@SKIPCREATE
ACLS:GOSUB@DIRTBG
NAME$="WORLD1":MN$=NAME$:SEED$=""
IF LANG==0THEN PNLSTR 8,0,"Create New World",0
IF LANG==1THEN PNLSTR 8,0,"Machen neuen Welt"
IF LANG==0THEN PNLSTR 6,2,"World Name",0
IF LANG==1THEN PNLSTR 6,2,"Welt Name"
GBOX 50,25,206,55,30
PNLSTR 7,5,MN$,0
GBOX 51,26,205,54,30
GFILL 52,27,204,53,31
IF LANG==0THEN PNLSTR 6,7,"Will be saved in:"+MN$,2
IF LANG==1THEN PNLSTR 6,7,"Wird in "+MN$+" sein"
PNLSTR 14,8,"GM "+STR$(GM)
IF LANG==0THEN PNLSTR 2,9,"Seed for the World Generator"
IF LANG==1THEN PNLSTR 2,9,"Saatgut fuer Welt-Generator"
GFILL 50,82,206,102,30
GFILL 52,84,204,100,31
PNLSTR 7,11,SEED$+"              ",0
IF LANG==0THEN PNLSTR 2,13,"Leave blank for a random seed",2
IF LANG==1THEN PNLSTR 2,13,"Leer lassen fuer Zufallsgenerator"
BX1=50:BY1=130:BX2=206:BY2=150:GOSUB@BTN
BX1=50:BY1=155:BX2=206:BY2=175:GOSUB@BTN
BX1=0:BY1=180:BX2=90:BY2=250:GOSUB@BTN
BX1=150:BX2=256:BY1=180:BY2=250:GOSUB@BTN
IF LANG==0THEN PNLSTR 8,17,"Create New World",0
IF LANG==1THEN PNLSTR 8,17,"Machen Neuen Welt"
IF LANG==0THEN PNLSTR 12,20,"Cancel",0
IF LANG==1THEN PNLSTR 12,20,"Stornieren"
IF LANG==0THEN PNLSTR 0,23," Load Save         World Options",0
IF LANG==1THEN PNLSTR 0,23," Last Welt         Welt Optionen"
GPAGE 0
FOR I=0TO 2:I=0
TT=TCHTIME:TX=TCHX:TY=TCHY
IF TT==1AND TX>=0AND TX<=90AND TY>=180AND TY<=250THEN GOTO @LOADSMAP
IF TT==1AND TX>=50AND TX<=206AND TY>=40AND TY<=70THEN GOSUB@NAMEINPUT
IF TT==1AND TX>=50AND TX<=206AND TY>=82AND TY<=102THEN GOSUB@SEEDINPUT
IF TT==1AND TX>=50AND TX<=206AND TY>=130AND TY<=150THEN I=2
IF TT==1AND TX>=50AND TX<=206AND TY>=155AND TY<=175GOTO@MENU
IF TT==1AND TX>=150AND TX2<=256AND TY>=180AND TY<=250THEN GOTO@MODE
VSYNC 1:NEXT:ACLS
@SKIPCREATE
FOR I=0TO 127
FOR J=0TO 127
MAP(I,J)=0
MAPB(I,J)=0
NEXT
NEXT
VISIBLE 1,0,0,0,0,0
IF GM!=2THEN GOSUB@TERRGEN
IF GM==2THEN MS=32
GBOX 0,0,MS-1,MS-1,10
GBOX 0,64,MS-1,MS+63,10
FOR I=0TO MS-1
FOR J=0TO MS-1
MAP(I,J)=GSPOIT(I,J)
MAPB(I,J)=GSPOIT(I,J+64)
NEXT
NEXT
GCLS
SEED$=SEED$+"N"
GN=1
GOSUB@TERRGEN
GBOX 0,0,MS-1,MS-1,10
GBOX 0,64,MS-1,MS+63,10
FOR I=0TO MS-1
FOR J=0TO MS-1
NETHER(I,J)=GSPOIT(I,J)
NETHERB(I,J)=GSPOIT(I,J+64)
NEXT
NEXT
GN=0
GCLS
@LOADMCXL
CAMX=RND(MS):CAMY=RND(MS)
IF GM==2THEN CAMX=1:CAMY=1
IF MAP(CAMX,CAMY)OR MAPB(CAMX,CAMY)GOTO@LOADMCXL
GCLS
IF GM==2THEN MOB_COUNT=100
FOR I=0TO MOB_COUNT-1
IF GM!=2THEN MOB_X(I)=RND(MS):MOB_Y(I)=RND(MS)
IF GM==2THEN MOB_X(I)=RND(28)+3:MOB_Y(I)=RND(28)+3
MOB_DIRECTION=RND(4)
MOB_LAYER(I)=RND(2)
IF MAP(MOB_X(I),MOB_Y(I))OR MAPB(MOB_X(I),MOB_Y(I))THEN I=I-1
NEXT
RETURN

@MODE
ACLS
GOSUB@DIRTBG
BX1=54:BX2=138:BY1=42+18:BY2=60+18:GOSUB@BTN
BY1=BY1+18:BY2=BY2+18:GOSUB@BTN
BY1=BY1+18:BY2=BY2+18:GOSUB@BTN
BX1=140:BX2=188:BY1=42+18:BY2=60+18:GOSUB@BTN
BY1=BY1+18:BY2=BY2+18:GOSUB@BTN
BY1=BY1+18:BY2=BY2+18:GOSUB@BTN
BX1=54:BX2=188:BY1=BY1+18:BY2=BY2+18:GOSUB@BTN
PNLSTR 7,8," Creative  Info"
PNLSTR 7,10," Survival  Info"
PNLSTR 7,13,"  Troll    Info"
PNLSTR 7,15,"     Cancel    "
@LP1
IF TCHST AND TCHX>=140AND TCHX<=188AND TCHY>=42+18AND TCHY<=60+18THEN PNLSTR 0,21,"Unlimited items"
IF TCHST AND TCHX>=52AND TCHX<=140AND TCHY>=42+18AND TCHY<=60+18THEN GM=0:GOTO@LOADMAP
IF TCHST AND TCHX>=140AND TCHX<=188AND TCHY>=42+36AND TCHY<=60+36THEN PNLSTR 0,21,"Not functional "
IF TCHST AND TCHX>=52AND TCHX<=140AND TCHY>=42+36AND TCHY<=60+36THEN GM=1:GOTO@LOADMAP
IF TCHST AND TCHX>=140AND TCHX<=188AND TCHY>=96AND TCHY<=96+18THEN PNLSTR 0,21,   "Don't hit stuff"
IF TCHST AND TCHX>=52AND TCHX<=140AND TCHY>=96AND TCHY<=96+18THEN GM=2:GOTO@LOADMAP
IF TCHST AND TCHX>=52AND TCHX<=188AND TCHY>=102+18AND TCHY<=102+36THEN GOTO@LOADMAP
GOTO@LP1

@BLOCKS
IF MENU THEN RETURN
IF DIST<=.5THEN RETURN
IF CAMB>=80THEN TDS=0ELSE TDS=1
GOSUB@FIST2
IF CAMA<=-7.8THEN BPOSX=-.5:BPOSY=-2
IF CAMA<-8.4THEN BPOSX=-2:BPOSY=-.5
IF CAMA<-10.2THEN BPOSX=-.5:BPOSY=1
IF CAMA<-11.8THEN BPOSX=1:BPOSY=-.5
IF CAMA<-13.4THEN BPOSX=-.5:BPOSY=-2
I=FLOOR(CAMX+BPOSX+.5)
J=FLOOR(CAMY+BPOSY+.5)
IF BUTTON()AND 512THEN BPS=1
IF BPS AND TDS AND MAP(I,J)==6AND !MENU THEN GOSUB@TNT
IF BPS AND TDS AND !MAP(I,J)AND !MENU THEN MAP(I,J)=BLOCK:BEEP 31
IF BPS AND TDS AND MAP(I,J)==22THEN MAPB(I,J)=23
IF BPS AND !TDS AND MAPB(I,J)==6AND !MENU THEN GOSUB@TNT
IF BPS AND !TDS AND !MAPB(I,J)AND !MENU THEN MAPB(I,J)=BLOCK:BEEP 31
IF BPS AND !TDS AND MAPB(I,J)==22THEN MAP(I,J)=22:MAPB(I,J)=23
IF BUTTON()AND 256THEN BRS=1
IF BRS AND TDS AND MAP(I,J)==35THEN GOSUB@HIT
IF BRS AND TDS AND MAP(I,J)==11THEN GOSUB@HIT
IF BRS AND TDS AND MAP(I,J)>0AND BREAKABLE(MAP(I,J)) THEN MAP(I,J)=0:BEEP 31
IF BRS AND TDS AND MAPB(I,J)==23THEN MAPB(I,J)=0
IF BRS AND !TDS AND MAP(I,J)==22THEN MAP(I,J)=0
IF BRS AND !TDS AND MAPB(I,J)==11THEN GOSUB@HIT
IF BRS AND !TDS AND MAPB(I,J)==36THEN GOSUB@HIT
IF BRS AND !TDS AND MAPB(I,J)>0AND BREAKABLE(MAPB(I,J)) THEN MAPB(I,J)=0:BEEP 31
IF TCHST AND TCHX>=150AND TCHY>=140AND TCHX<=197AND TCHY<=154THEN BSTAT=1
IF TCHST AND TCHX>=206AND TCHY>=140AND TCHX<=252AND TCHY<=154THEN PSTAT=1
IF BSTAT AND TDS AND MAP(I,J)AND BREAKABLE(MAP(I,J))THEN MAP(I,J)=0:BEEP 31
IF BSTAT AND !TDS AND MAPB(I,J)AND BREAKABLE(MAPB(I,J))THEN MAPB(I,J)=0:BEEP 31
IF PSTAT AND TDS AND !MAP(I,J)THEN MAP(I,J)=BLOCK:BEEP 31
IF PSTAT AND !TDS AND !MAPB(I,J)THEN MAPB(I,J)=BLOCK:BEEP 31
IF BLOCK==28THEN GOSUB@PORTALC
IF MAP(I,J)==8OR MAP(I,J)==9THEN IF !MAPB(I,J)THEN BNCF=1
BSTAT=0:PSTAT=0
BPS=0:BRS=0
BSI=I
BSJ=J
RETURN

@PORTALC
IF I<2THEN RETURN
IF J<2THEN RETURN
UTV1=(MAP(I-2,J)==28)AND (MAPB(I-2,J)==28)AND (MAP(I,J)==28)AND (MAPB(I,J)==28)
UTV2=(MAP(I+2,J)==28)AND (MAPB(I+2,J)==28)AND (MAP(I,J)==28)AND (MAPB(I,J)==28)
UTV3=(MAP(I,J-2)==28)AND (MAPB(I,J-2)==28)AND (MAP(I,J)==28)AND (MAPB(I,J)==28)
UTV4=(MAP(I,J+2)==28)AND (MAPB(I,J+2)==28)AND (MAP(I,J)==28)AND (MAPB(I,J)==28)
IF UTV1 THEN MAP(I-1,J)=18:MAPB(I-1,J)=18
IF UTV2 THEN MAP(I+1,J)=18:MAPB(I+1,J)=18
IF UTV3 THEN MAP(I,J-1)=18:MAPB(I,J-1)=18
IF UTV4 THEN MAP(I,J+1)=18:MAPB(I,J+1)=18
RETURN

@FIST2
SPSET 0,30,HANDY,0,0,0,64,64
SPHOME 0,-128,-128
BGFILL 0,15,17,15,17,7,HANDY,0,0
BGFILL 0,14,18,14,18,38,HANDY,0,0
BGFILL 0,15,18,15,18,39,HANDY,0,0
BGFILL 0,13,19,13,19,69,HANDY,0,0
BGFILL 0,14,19,14,19,70,HANDY,0,0
BGFILL 0,15,19,15,19,71,HANDY,0,0
BGFILL 0,13,20,13,20,101,HANDY,0,0
BGFILL 0,14,20,14,20,102,HANDY,0,0
BGFILL 0,15,20,15,20,103,HANDY,0,0
BGFILL 0,14,21,14,21,134,HANDY,0,0
BGFILL 0,15,21,15,21,135,HANDY,0,0
BGFILL 0,15,22,15,22,167,HANDY,0,0
RETURN

@HIT
BEEP 11
RETURN

@PAUSE
ACLS
CHRINIT "BGF0"
LOCATE 0,8
?"    ˜•™ ˜•™ – – ˜•™ ˜•• ˜™ "
?"    – – – – – – –   –   –š™"
?"    ’•› ’•” – – š•™ ’•  – –"
?"    –   – – – –   – –   –˜›"
?"    –   – – š•› š•› š•• š› "
PNLSTR 8,8,"  Quit and Save  "
PNLSTR 8,12,"    Load  Map    "
PNLSTR 8,16,"     Resume      "
GPAGE 1:GOSUB@DIRTBG
BX1=65:BY1=60:BX2=200:BY2=80:GOSUB@BTN
BX1=65:BY1=90:BX2=200:BY2=110:GOSUB@BTN
BX1=65:BY1=120:BX2=200:BY2=145:GOSUB@BTN
GPAGE 0
@PL
IF TCHST AND TCHX>=65AND TCHY>=20AND TCHX<=200AND TCHY<=50GOTO@HHS
IF TCHST AND TCHX>=65AND TCHY>=60AND TCHX<=200AND TCHY<=75GOTO@SAVEMAP
IF TCHST AND TCHX>=65AND TCHY>=90AND TCHX<=200AND TCHY<=105GOTO@LOADSMAP
IF TCHST AND TCHX>=65AND TCHY>=130AND TCHX<=200AND TCHY<=145THEN TRS=1
IF TRS THEN TRS=0:GOSUB@RESUME:GOSUB@TGUIS:GOTO@LOOP1
GOTO@PL

@SAVEMAP
ACLS
IF !INNETHER THEN GOTO@SAVEMAPS
FOR I=0TO 127
FOR J=0TO 127
IF MAP(I,J)!=34THEN SWAP MAP(I,J),NETHER(I,J)ELSE MAP(I,J)=0
IF MAPB(I,J)!=34THEN SWAP MAPB(I,J),NETHERB(I,J)ELSE MAPB(I,J)=0
NEXT
NEXT
SWAP CAMX,STOREDX
SWAP CAMY,STOREDY
@SAVEMAPS
FOR I=0TO 63
FOR J=0TO 63
GPSET J,I,MAP(J,I)
GPSET J,I+64,MAPB(J,I)
GPSET J+64,I+64,NETHER(J,I)
GPSET J+128,I+64,NETHERB(J,I)
NEXT
NEXT
GPSET 129,0,FLOOR(CAMX+.5)
GPSET 130,0,FLOOR(CAMY+.5)
GPSET 129,1,FLOOR(CAMZ+.5)
GPSET 130,1,ABS(FLOOR(CAMA+.5))
GPSET 129,2,ABS(FLOOR(CAMB+.5))
GPSET 129,3,STOREDX
GPSET 129,4,STOREDY
FOR I=0TO 5
GPSET 131,I,B(I)
NEXT
GPSET 130,2,IIH
FOR I=0TO LEN(VERS$)-1
GPSET 132+I,0,ASC(MID$(VERS$,I,1))
NEXT
FOR I=0TO LEN(NICK$)-1
GPSET 132+I,1,ASC(MID$(NICK$,I,1))
NEXT
GPSET 132,2,LEN(NICK$)-1
GPSET 132,3,MOB_ENABLED
FOR I=0TO 4
GPSET 132+I,4,MOB_X(I)
GPSET 132+I,5,MOB_Y(I)
GPSET 132+I,6,MOB_LAYER(I)
NEXT
GPSET 137,4,MOB_COUNT
GPSET 138,4,Q
SYSBEEP=0
SAVE"GRP:"+NAME$
SYSBEEP=1
GOTO@HHS

@LOADSMAP
INPUT "Save Name";NAME$
SYSBEEP=0
LOAD"GRP:"+NAME$,0
SYSBEEP=1
MVS$=""
CRST=0
IF !GSPOIT(132,0)THEN MVS$="null":GOSUB@INVV:WAIT 360:GOSUB@RESUME:GOTO@LOOP1
FOR I=0TO 9
MVS$=MVS$+CHR$(GSPOIT(132+I,0))
NEXT
IF MVS$!=VERS$ THEN GOSUB@INVV:GOSUB@RESUME:GOTO@LOOP1
NICKLEN=GSPOIT(132,2)
NICK$=""
FOR I=0TO NICKLEN
NICK$=NICK$+CHR$(GSPOIT(132+I,1))
NEXT
FOR J=0TO 63
FOR I=0TO 63
MAP(I,J)=GSPOIT(I,J)
MAPB(I,J)=GSPOIT(I,J+64)
NETHER(I,J)=GSPOIT(I+64,J+64)
NETHERB(I,J)=GSPOIT(I+128,J+64)
NEXT
NEXT
CAMX=GSPOIT(129,0)
CAMY=GSPOIT(130,0)
CAMZ=GSPOIT(129,1)
CAMA=0-GSPOIT(130,1)
CAMB=0-GSPOIT(129,2)
STOREDX=GSPOIT(129,3)
STOREDY=GSPOIT(129,4)
FOR I=0TO 4
MOB_X(I)=GSPOIT(132+I,4)
MOB_Y(I)=GSPOIT(132+I,5)
MOB_LAYER(I)=GSPOIT(132+I,6)
NEXT
MOB_COUNT=GSPOIT(137,4)
Q=GSPOIT(138,4)
FOR I=0TO 5
B(I)=GSPOIT(131,I)
NEXT
IIH=GSPOIT(130,2)
BLOCK=B(IIH-1)
CLS
INVST=1
GOSUB@TEXT
GOSUB@INVBSU
GOTO@LOOP1

@RESUME
ACLS
SYSBEEP=0
LOAD"BGF:BOLDITAL",FALSE
SYSBEEP=1
RETURN

@TEXT
ACLS
SYSBEEP=0
LOAD"BGF:BOLDITAL",FALSE
'-----Change IGN$ to change your name
IGN$="IAmAPerson620"
'-----Change RANK$ to change your rank (User, Colored, Staff, Admin, Owner)
RANK$="Owner"
IF RANK$=="User" THEN RI=1
IF RANK$=="Colored" THEN RI=2
IF RANK$=="Staff" THEN RI=3
IF RANK$=="Admin" THEN RI=4
IF RANK$=="Owner" THEN RI=5
IF !NICKLEN THEN NICK$=IGN$
@TS
VST=0
SYSBEEP=1
COLOR 7:?"Welcome back, ";:COLOR 13:? IGN$;:COLOR 7:?"!"
COLOR 7:?"You are a(n) ";:COLOR 13:? RANK$;:COLOR 7:?"."
COLOR 7:?"Type ";:COLOR 13:?"/help";:COLOR 7:?" for a list of"
COLOR 7:?"commands."
COLOR 0
PNLTYPE"OFF"
RETURN

@S
PNLTYPE"OFF"
GOSUB@TGUIS
SYSBEEP=0:LOAD"BGF:BOLDITAL",0:SYSBEEP=1
GOSUB@LOOP1
RETURN
@S2
ACLS
LINPUT TXTI$
IF RI>=3THEN IF TXTI$=="/nick"THEN GOTO@NICK
IF RI>=4THEN IF TXTI$=="/ban"THEN GOTO@BAN
IF RI>=3THEN IF TXTI$=="/tempban"THEN GOTO@TBAN
IF RI>=1THEN IF TXTI$=="/leave"THEN GOTO@LEAVE
IF RI>=3THEN IF TXTI$=="/kick"THEN GOTO@KICK
IF RI>=3THEN IF TXTI$=="/mute"THEN GOTO@MUTE
IF RI>=1THEN IF TXTI$=="/help"THEN GOTO@HELP
IF RI==5THEN IF TXTI$=="/broadcast"THEN GOTO@BC
IF RI>=1THEN IF TXTI$=="/me"THEN GOTO@ME
IF RI>=1THEN IF TXTI$=="/ranks"THEN GOTO@RANKS
IF RI>=4THEN IF TXTI$=="/unban"THEN GOTO@UNBAN
IF RI>=4THEN IF TXTI$=="/vanish"THEN GOTO@V
IF RI==5THEN IF TXTI$=="/rankup"THEN GOTO@RU
IF RI>=1THEN IF TXTI$=="/quality"THEN GOTO@QSET
IF RI>=1THEN IF TXTI$=="/data"THEN GOTO@DATA
IF RANK$=="User"THEN GOTO@UT
IF RANK$=="Colored"THEN TXT$="&8<&4C&6o&2l&3o&1r&5e&4d&8>&6 "+IGN$+" &7>"+"&r "+TXTI$
IF RANK$=="Staff"THEN TXT$="&8<&3Staff&8>&3 "+NICK$+" &7>"+"&3 "+TXTI$
IF RANK$=="Admin"THEN TXT$="&8<&4Admin&8>&4 "+NICK$+" &7>"+"&4 "+TXTI$
IF RANK$=="Owner"THEN TXT$="&8<<&4&oOwner&8>>&4 "+NICK$+" &7>"+"&4 "+TXTI$
@CHATCOLOR
GOSUB@COLORTXT
PRINT""
COLOR 0
GOTO@S
@COLORTXT
N=-1
TXTLN=LEN(TXT$)
@LOOP
N=N+1
IF N>=TXTLN THEN GOTO@SKIP ELSE T$=MID$(TXT$,N,1)
IF T$=="&" THEN GOTO@COLR
PRINT T$;
GOTO@LOOP
@SKIP
RETURN
@COLR
CLL$=MID$(TXT$,N+1,1)
IF CLL$=="0"THEN COLN=0
IF CLL$=="1"THEN COLN=11
IF CLL$=="2"THEN COLN=4
IF CLL$=="3"THEN COLN=9
IF CLL$=="4"THEN COLN=13
IF CLL$=="5"THEN COLN=10
IF CLL$=="6"THEN COLN=7
IF CLL$=="7"THEN COLN=2
IF CLL$=="8"THEN COLN=14
IF CLL$=="9"THEN COLN=9
IF CLL$=="r"THEN COLN=0
IF CLL$=="l"THEN GOTO @BOLD
IF CLL$=="o"THEN GOTO @ITALICS
COLOR COLN
N=N+1
@FFTLT
GOTO@LOOP
@HELP
COLOR 13
PRINT"------------Commands------------"
COLOR 7
IF RI>=3 THEN PRINT "/kick <name> <reason>
IF RI>=3 THEN PRINT "/tempban <name> <time> <reason>"
IF RI>=4 THEN PRINT "/ban <name>"
IF RI>=3 THEN PRINT "/nick <nickname>"
IF RI>=3 THEN PRINT "/mute <name>"
IF RI==5 THEN PRINT "/broadcast <message>"
IF RI>=1 THEN PRINT "/me <action>"
IF RI>=4 THEN PRINT "/unban <name>"
IF RI>=4 THEN PRINT "/vanish"
IF RI>=1 THEN PRINT "/ranks"
IF RI==5 THEN PRINT "/rankup <name> <rank>"
IF RI>=1 THEN PRINT "/leave"
IF RI>=1 THEN PRINT "/quality <min. 1>
IF RI>=1 THEN PRINT "/data"
PRINT""
COLOR 13
PRINT"--------------------------------"
COLOR 0
GOTO @S
@KICK
COLOR 13:PRINT "NAME?":COLOR 0
INPUT N$
COLOR 13:PRINT "REASON?":COLOR 0
INPUT R$
TXT$="&6Player&4 "+NICK$+" &6kicked&4 "+N$+" &6for "+R$+"."
GOSUB @COLORTXT
COLOR 0
PRINT ""
IF N$==IGN$ THEN GOTO @LEAVE
GOTO @S
@TBAN
COLOR 13:PRINT "NAME?":COLOR 0
INPUT N$
@TBT
COLOR 13:PRINT "TIME (HOURS)?":COLOR 0
INPUT T
IF T>168 THEN GOTO @TBTE
COLOR 13:PRINT "REASON?":COLOR 0
INPUT R$
IF N$==IGN$ THEN GOTO @NB
TXT$="&6Player&4 "+NICK$+" &6banned&4 "+N$+" &6for&4 "+STR$(T)+" &6hours for "+R$+"."
GOSUB @COLORTXT
COLOR 0
PRINT ""
GOTO @S
@NICK
COLOR 13:PRINT "NICKNAME?":COLOR 0
INPUT NICK$
TXT$="&6Your nickname is now&4 "+NICK$+"&6."
GOSUB @COLORTXT
COLOR 0:?""
GOTO @S
@BAN
COLOR 13:?"NAME?":COLOR 0
INPUT N$
IF N$==IGN$ THEN GOTO @NB
TXT$="&6Player&4 "+NICK$+" &6banned&4 "+N$+" &6for the Ban Hammer has spoken!"
GOSUB @COLORTXT
COLOR 0
PRINT ""
GOTO @S
@MUTE
COLOR 13:?"NAME?":COLOR 0
INPUT N$
IF N$==IGN$ THEN GOTO @NM
TXT$="&6Player&4 "+NICK$+" &6muted&4 "+N$+"&6."
GOSUB @COLORTXT
COLOR 0
PRINT ""
GOTO @S
@LEAVE
TXT$="&6Player&4 "+NICK$+" &6left the game."
GOSUB @COLORTXT
WAIT 180
COLOR 0
END
@BC
COLOR 13:?"MESSAGE?":COLOR 0
INPUT M$
TXT$="&6[&4BROADCAST&6]&4 "+M$
GOSUB @COLORTXT
COLOR 0
PRINT ""
GOTO @S
@ME
COLOR 13:?"ACTION?":COLOR 0
INPUT A$
TXT$="&5* "+NICK$+"&5 "+A$
GOSUB @COLORTXT
COLOR 0
PRINT ""
GOTO @S
@RANKS
COLOR 13
PRINT "-------------Ranks-------------"
PRINT ""
TXT$="&rUser Rank:    &7<&8Name&7>"
GOSUB @COLORTXT
PRINT ""
TXT$="&rColored Rank: &8<&4C&6o&2l&3o&1r&5e&4d&8> &6Name &7>"
GOSUB @COLORTXT
PRINT ""
TXT$="&rStaff Rank:   &8<&3Staff&8> &3Name &7>"
GOSUB @COLORTXT
PRINT ""
TXT$="&rAdmin Rank:   &8<&4Admin&8> &4Name &7>"
GOSUB @COLORTXT
PRINT ""
TXT$="&rOwner Rank:   &8<<&4&oOwner&8>> &4Name &7>"
GOSUB @COLORTXT
PRINT ""
PRINT ""
COLOR 13
PRINT "-------------------------------"
COLOR 0
GOTO @S
@UNBAN
COLOR 13:?"NAME?":COLOR 0
INPUT N$
TXT$="&6Player&4 "+NICK$+" &6unbanned&4 "+N$+"&6."
GOSUB @COLORTXT
PRINT ""
COLOR 0
GOTO @S
@V
IF VST THEN GOTO @VRA
COLOR 7:?"YOU ARE NOW VANISHED FROM ALL"
PRINT "NORMAL PLAYERS. ANY KICKS/BANS"
PRINT "WILL REMAIN ANONYMOUS. DO"
PRINT "/vanish AGAIN TO REAPPEAR"
VST=1
COLOR 0
GOTO @S
@VRA
COLOR 7:?"YOU HAVE REAPPEARED"
VST=0
COLOR 0
GOTO @S
@UT
COLOR 2:?"<";:COLOR 14:? IGN$;:COLOR 2:?">";:COLOR 14:?" " TXTI$
COLOR 0
GOTO @S
@NB
COLOR 13:?"YOU CANNOT BAN YOURSELF!"
COLOR 0
GOTO @S
@TBTE
COLOR 13:?"YOU CANNOT TEMPBAN FOR LONGER"
PRINT "THAN A WEEK!"
GOTO @TBT
@RU
COLOR 13:?"NAME?":COLOR 0
INPUT N$
IF N$==IGN$ THEN GOTO @RUY
@RURI
COLOR 13:PRINT "RANK?"
COLOR 0
INPUT RURI$
IF RURI$!="User" THEN IF RURI$!="Colored" THEN IF RURI$!="Staff" THEN IF RURI$!="Admin" THEN @IR
TXT$="&6Player&4 "+N$+" &6is now a(n) "+RURI$+"!"
GOSUB @COLORTXT
PRINT ""
COLOR 0
GOTO @S
@IR
COLOR 13:?"THE RANK '" RURI$ "' IS EITHER AN UNKNOWN RANK OR THIS PLAYER CANNOT BE THIS RANK."
PRINT "PLEASE USE 'User', 'Colored',"
PRINT "'Staff', OR 'Admin'."
COLOR 0
GOTO @S
@RUY
COLOR 13:?"YOU CANNOT CHANGE YOUR OWN RANK!":COLOR 0
GOTO @S
@BOLD
N=N+1
BPT$=""
NBL$=""
@BOLDLOOP
N=N+1
BPTO$=BPT$
NBL$=MID$(TXT$,N,1)
IF NBL$=="&" THEN GOTO @BOLDEND
IF NBL$=="A" THEN BPT$=BPT$+"" ELSE IF NBL$=="B" THEN BPT$=BPT$+"‚"
IF NBL$=="C" THEN BPT$=BPT$+"ƒ" ELSE IF NBL$=="D" THEN BPT$=BPT$+"„"
IF NBL$=="E" THEN BPT$=BPT$+"…" ELSE IF NBL$=="F" THEN BPT$=BPT$+"†"
IF NBL$=="G" THEN BPT$=BPT$+"‡" ELSE IF NBL$=="H" THEN BPT$=BPT$+"ˆ"
IF NBL$=="I" THEN BPT$=BPT$+"‰" ELSE IF NBL$=="J" THEN BPT$=BPT$+"Š"
IF NBL$=="K" THEN BPT$=BPT$+"‹" ELSE IF NBL$=="L" THEN BPT$=BPT$+"Œ"
IF NBL$=="M" THEN BPT$=BPT$+"" ELSE IF NBL$=="N" THEN BPT$=BPT$+"Ž"
IF NBL$=="O" THEN BPT$=BPT$+"" ELSE IF NBL$=="P" THEN BPT$=BPT$+""
IF NBL$=="Q" THEN BPT$=BPT$+"‘" ELSE IF NBL$=="R" THEN BPT$=BPT$+"’"
IF NBL$=="S" THEN BPT$=BPT$+"“" ELSE IF NBL$=="T" THEN BPT$=BPT$+"”"
IF NBL$=="U" THEN BPT$=BPT$+"•" ELSE IF NBL$=="V" THEN BPT$=BPT$+"–"
IF NBL$=="W" THEN BPT$=BPT$+"—" ELSE IF NBL$=="X" THEN BPT$=BPT$+"˜"
IF NBL$=="Y" THEN BPT$=BPT$+"™" ELSE IF NBL$=="Z" THEN BPT$=BPT$+"š"
IF NBL$=="a" THEN BPT$=BPT$+"¡" ELSE IF NBL$=="b" THEN BPT$=BPT$+"¢"
IF NBL$=="c" THEN BPT$=BPT$+"£" ELSE IF NBL$=="d" THEN BPT$=BPT$+"¤"
IF NBL$=="e" THEN BPT$=BPT$+"¥" ELSE IF NBL$=="f" THEN BPT$=BPT$+"¦"
IF NBL$=="g" THEN BPT$=BPT$+"§" ELSE IF NBL$=="h" THEN BPT$=BPT$+"¨"
IF NBL$=="i" THEN BPT$=BPT$+"©" ELSE IF NBL$=="j" THEN BPT$=BPT$+"ª"
IF NBL$=="k" THEN BPT$=BPT$+"«" ELSE IF NBL$=="l" THEN BPT$=BPT$+"¬"
IF NBL$=="m" THEN BPT$=BPT$+"­" ELSE IF NBL$=="n" THEN BPT$=BPT$+"®"
IF NBL$=="o" THEN BPT$=BPT$+"¯" ELSE IF NBL$=="p" THEN BPT$=BPT$+"»"
IF NBL$=="q" THEN BPT$=BPT$+"±" ELSE IF NBL$=="r" THEN BPT$=BPT$+"²"
IF NBL$=="s" THEN BPT$=BPT$+"³" ELSE IF NBL$=="t" THEN BPT$=BPT$+"´"
IF NBL$=="u" THEN BPT$=BPT$+"µ" ELSE IF NBL$=="v" THEN BPT$=BPT$+"¶"
IF NBL$=="w" THEN BPT$=BPT$+"·" ELSE IF NBL$=="x" THEN BPT$=BPT$+"¸"
IF NBL$=="y" THEN BPT$=BPT$+"¹" ELSE IF NBL$=="z" THEN BPT$=BPT$+"º"
IF N>=LEN(TXT$) THEN GOTO @BOLDEND
IF LEN(BPTO$)==LEN(BPT$) THEN BPT$=BPT$+NBL$
GOTO @BOLDLOOP
@BOLDEND
PRINT BPT$;
IF NBL$=="&" THEN N=N-1
IF N>=LEN(TXT$) THEN RETURN ELSE GOTO @FFTLT
@ITALICS
N=N+1
IPT$=""
NIT$=""
@ITALLOOP
N=N+1
IPTO$=IPT$
NIT$=MID$(TXT$,N,1)
IF NIT$=="&" THEN GOTO @ITALEND
IF NIT$=="A" THEN IPT$=IPT$+"Á" ELSE IF NIT$=="B" THEN IPT$=IPT$+"Â"
IF NIT$=="C" THEN IPT$=IPT$+"Ã" ELSE IF NIT$=="D" THEN IPT$=IPT$+"Ä"
IF NIT$=="E" THEN IPT$=IPT$+"Å" ELSE IF NIT$=="F" THEN IPT$=IPT$+"Æ"
IF NIT$=="G" THEN IPT$=IPT$+"Ç" ELSE IF NIT$=="H" THEN IPT$=IPT$+"È"
IF NIT$=="I" THEN IPT$=IPT$+"É" ELSE IF NIT$=="J" THEN IPT$=IPT$+"Ê"
IF NIT$=="K" THEN IPT$=IPT$+"Ë" ELSE IF NIT$=="L" THEN IPT$=IPT$+"Ì"
IF NIT$=="M" THEN IPT$=IPT$+"Í" ELSE IF NIT$=="N" THEN IPT$=IPT$+"Î"
IF NIT$=="O" THEN IPT$=IPT$+"Ï" ELSE IF NIT$=="P" THEN IPT$=IPT$+"Ð"
IF NIT$=="Q" THEN IPT$=IPT$+"Ñ" ELSE IF NIT$=="R" THEN IPT$=IPT$+"Ò"
IF NIT$=="S" THEN IPT$=IPT$+"Ó" ELSE IF NIT$=="T" THEN IPT$=IPT$+"Ô"
IF NIT$=="U" THEN IPT$=IPT$+"Õ" ELSE IF NIT$=="V" THEN IPT$=IPT$+"Ö"
IF NIT$=="W" THEN IPT$=IPT$+"×" ELSE IF NIT$=="X" THEN IPT$=IPT$+"Ø"
IF NIT$=="Y" THEN IPT$=IPT$+"Ù" ELSE IF NIT$=="Z" THEN IPT$=IPT$+"Ú"
IF NIT$=="a" THEN IPT$=IPT$+"á" ELSE IF NIT$=="b" THEN IPT$=IPT$+"â"
IF NIT$=="c" THEN IPT$=IPT$+"ã" ELSE IF NIT$=="d" THEN IPT$=IPT$+"ä"
IF NIT$=="e" THEN IPT$=IPT$+"å" ELSE IF NIT$=="f" THEN IPT$=IPT$+"æ"
IF NIT$=="g" THEN IPT$=IPT$+"ç" ELSE IF NIT$=="h" THEN IPT$=IPT$+"è"
IF NIT$=="i" THEN IPT$=IPT$+"é" ELSE IF NIT$=="j" THEN IPT$=IPT$+"ê"
IF NIT$=="k" THEN IPT$=IPT$+"ë" ELSE IF NIT$=="l" THEN IPT$=IPT$+"ì"
IF NIT$=="m" THEN IPT$=IPT$+"í" ELSE IF NIT$=="n" THEN IPT$=IPT$+"î"
IF NIT$=="o" THEN IPT$=IPT$+"ï" ELSE IF NIT$=="p" THEN IPT$=IPT$+"ð"
IF NIT$=="q" THEN IPT$=IPT$+"ñ" ELSE IF NIT$=="r" THEN IPT$=IPT$+"ò"
IF NIT$=="s" THEN IPT$=IPT$+"ó" ELSE IF NIT$=="t" THEN IPT$=IPT$+"ô"
IF NIT$=="u" THEN IPT$=IPT$+"õ" ELSE IF NIT$=="v" THEN IPT$=IPT$+"ö"
IF NIT$=="w" THEN IPT$=IPT$+"÷" ELSE IF NIT$=="x" THEN IPT$=IPT$+"ø"
IF NIT$=="y" THEN IPT$=IPT$+"ù" ELSE IF NIT$=="z" THEN IPT$=IPT$+"ú"
IF N>=LEN(TXT$) THEN GOTO @ITALEND
IF LEN(IPTO$)==LEN(IPT$) THEN IPT$=IPT$+NIT$
GOTO @ITALLOOP
@ITALEND
PRINT IPT$;
IF NIT$=="&" THEN N=N-1
IF N>=LEN(TXT$) THEN RETURN ELSE GOTO @FFTLT
@QSET
COLOR 13:PRINT "QUALITY (min. 1)?":COLOR 0
INPUT QUALITY$
Q=VAL(QUALITY$)
IF !Q THEN Q=6
GOTO @S
@TXTRESIZ
COLOR 13:?"SIZE (8 or 16)?":COLOR 0
INPUT TXTRESIZ$
IF VAL(TXTRESIZ$)!=8 THEN IF VAL(TXTRESIZ$)!=16 THEN TXTRESIZ$="8"
GOTO @S
@DATA
COLOR 13
CLS
PRINT "X=" X " Y=" Y
PRINT "User=" IGN$
PRINT "Map size=" MS "x" MS
COLOR 0
GOTO @S

@SCREENSHOT
CLS
VISIBLE 1,0,0,0,0,0
LOCATE 6,11:PRINT "Taking screenshot"
IF PAGE==3THEN PAGE=2:GPAGE 0,3,2 ELSE PAGE=3:GPAGE 0,2,3
FOR I=0TO 255
FOR J=0TO 191
SCREENSHOT(I,J)=GSPOIT(I,J)
NEXT
NEXT
GCLS
SPCLR
GPAGE 0,0,0
FOR I=0TO 255
FOR J=0TO 191
GPSET I,J,SCREENSHOT(I,J)
NEXT
NEXT
CLS
VISIBLE 1,1,1,1,1,1
COLOR 1
LINPUT"Save screenshot? (Y/N)";SSO$
IF SSO$!="Y"THEN GOSUB @RESUME:RETURN
LINPUT"Name?";SSN$
SSN$="GRP:"+SSN$
SAVE SSN$
GOSUB@RESUME
GOSUB@TGUIS
RETURN

@MENU
MENU=1
RESTORE @SPLSHDAT
FOR S=0TO 58
READ SPLASH$(S)
NEXT

@SPLSHDAT
DATA"Post-Alpha!","Talking boot!","NintendoLife!","BonChoi Rocks!","IAmAPerson!","damolii!"
DATA"SYNTAX ERROR!","Open-Sourced!","Blah, Blah!","Hobbiton!","Read Books!","deja vu!"
DATA"üöìçäÿ’þ™ö!","263 Pages!","No Dragons!","FUS RO DAH!","ABC123!","Beta!"
DATA"Minecraft!","Bootstrap!","C++ Free!","randomouscrap!","Potato!","Big Red Button!"
DATA"Made in USA!","NCIS!","Saveable!","Free!","Loved by Yoshi!","Flashlight-Friendly!"
DATA"Free of Mice!","Nap Time!","Buy 2 Get 1 Free!","Banana!","Unbroken!"
DATA"99% Bug-Free!","U mad bro?","Lovin Tater Tots!","Right Behind You!","Stop!"
DATA"Not Too Laggy!","47952 Lines of Code!","Oh, man!","Bombs away!","Ricochet!"
DATA"Small Block Chevy!","Window of Fate!","Morphtorok!","Waffles!","007!","Moonwalk!"
DATA"Do the Harlem Shake!","Gangnam Style!","Houston, problem!","Tell IAmAPerson if you see this!"
DATA"LeviCelJir!","Slimes!","Steve PAWNCH!","Donkeys!"
ACLS
COLOR 1
LOCATE 0,4
PRINT "˜™˜™ •‘• ˜™–˜• ˜• ˜•™ ˜•™ ˜• •‘•"
LOCATE 0,5:PRINT "––––  –  ––––  –  – – – – –   –"
PRINT "–š›–  –  –––’• –  ’‘› ’•” ’•  –"
PRINT "–  –  –  ––––  –  –š™ – – –   –"
PRINT "–  – •• –š›š• š• – – – – –   –" 
PRINT ""
PRINT "           ———   œ——"
PRINT "           —  ž œŸ  Ÿ"
PRINT "           —   — —    "
PRINT "           —   — ž   "
PRINT "           —   —  ž— "
PRINT "           —   —    ž"
PRINT "           —   —     —"
PRINT "           —  œŸ œ  œŸ"
PRINT "           ———Ÿ  ž——Ÿ "
COLOR 3
SI=RND(59)
LOCATE 31-LEN(SPLASH$(SI)),20
PRINT SPLASH$(SI):GOSUB@DIRTBG
COLOR 0:LOCATE 0,22:?VERS$
GPAGE 1:
BX1=65:BY1=68:BX2=200:BY2=85:GOSUB@BTN
BX1=65:BY1=100:BX2=200:BY2=115:GOSUB@BTN
BX1=198:BY1=180:BX2=300:BY2=250:GOSUB@BTN
BX1=0:BY1=180:BX2=90:BY2=250:GOSUB@BTN
GPAGE 0
IF LANG==0THEN PNLSTR 8,9,"   Single-Player",0
IF LANG==1THEN PNLSTR 8,9,"     Beginnen  ",0
IF LANG==0THEN PNLSTR 8,13,"     Options     ",0
IF LANG==1THEN PNLSTR 8,13,"     Optionen    ",0
IF LANG==0THEN PNLSTR 24,23," Credits",0
IF LANG==1THEN PNLSTR 24,23," Kredit ",0
IF LANG==0THEN PNLSTR 0,23," Close Game",0
IF LANG==1THEN PNLSTR 0,23,"Schleissen ",0

GOSUB@LOOP1
@STARTTEST
TX=TCHX
TY=TCHY
IF TX>=65AND TY>=68AND TX<=200AND TY<=85GOTO@SSETUP
IF TX>=65AND TY>=100AND TX<=200AND TY<=115GOTO@TBC
IF TX>=198AND TY>=183AND TCHST GOTO@CREDITS
IF TX>=0AND TY>=180AND TX<=90AND TY<=250 THEN END
GOSUB@MAINLOOP
GOTO@STARTTEST

@TBC
WAIT 10:ACLS
GPAGE 1
GOSUB@DIRTBG
BX1=1:BY1=1:BX2=126:BY2=20:GOSUB@BTN
BX1=1:BY1=23:BX2=126:BY2=43:GOSUB@BTN
BX1=129:BY1=1:BX2=254:BY2=20:GOSUB@BTN
BX1=129:BY1=23:BX2=254:BY2=43:GOSUB@BTN
BX1=1:BY1=46:BX2=126:BY2=70:GOSUB@BTN
BX1=1:BY1=73:BX2=126:BY2=90:GOSUB@BTN
BX1=129:BY1=46:BX2=254:BY2=70:GOSUB@BTN
BX1=129:BY1=73:BX2=254:BY2=90:GOSUB@BTN
BX1=65:BY1=120:BX2=200:BY2=145:GOSUB@BTN
BX1=1:BY1=172:BX2=254:BY2=187:GOSUB@BTN
GPAGE 0
LOCATE 0,9
IF LANG==0THEN ?"  ˜•™ ˜•™ •‘• •‘• ˜•™ ˜™ – ˜•™"
IF LANG==0THEN ?"  – – – –  –   –  – – –– – –  "
IF LANG==0THEN ?"  – – ’•›  –   –  – – –š™– š•™"
IF LANG==0THEN ?"  – – –    –   –  – – – ––   –"
IF LANG==0THEN ?"  š•› –    –  •• š•› – š› š•›"
IF LANG==1THEN ?"˜•™ ˜•™•‘• •‘• ˜•™ ˜™ – ˜• ˜™ –"
IF LANG==1THEN ?"– – – – –   –  – – –– – –  –– –"
IF LANG==1THEN ?"– – ’•› –   –  – – –š™– ’• –š™–"
IF LANG==1THEN ?"– – –   –   –  – – – –– –  – ––"
IF LANG==1THEN ?"š•› –   –  •• š•› – š› š• – š›"
IF LANG==0THEN PNLSTR 0,1," Map Size:  x      Slimes       "
IF LANG==1THEN PNLSTR 0,1," Karte:     x      Slimen       "
IF LANG==0THEN PNLSTR 0,4," Texture  Packs     Mods        "
IF LANG==1THEN PNLSTR 0,4," Textur-Packs Aenderungen       "
IF LANG==0THEN PNLSTR 0,7," Time Cycle      Slime Count:   "
IF LANG==1THEN PNLSTR 0,7," Zeitzyklus      Slimen Graf:   "
IF LANG==0THEN PNLSTR 0,10," Video Settings       Skin     "
IF LANG==1THEN PNLSTR 0,10," Grafik-Optionen      Haut     "
IF LANG==0THEN PNLSTR 8,16,"      Back       "
IF LANG==1THEN PNLSTR 8,16,"   Schleissen    "
PNLSTR 0,22," Language/Sprache/Langue/¸Þ-Â¼Þ"
PNLSTR 10,1,STR$(MS)
PNLSTR 13,1,STR$(MS)
PNLSTR 25,4,MSS$
PNLSTR 26,1,SSS$
PNLSTR 12,7,TSS$
PNLSTR 30,7,STR$(MOB_COUNT)
@TBTEST
TX=TCHX
TY=TCHY
TS=TCHST

IF TS AND TX>=0AND TY>=0AND TX<=128AND TY<=20GOTO@MSC
IF TX>=0AND TY>=21AND TX<=128AND TY<=41GOTO@TBLC
IF TS AND TX>=129AND TY>=0AND TX<=255AND TY<=20GOTO@SOC
IF TS AND TX>=129AND TY>=25AND TX<=255AND TY<=41GOTO@MOC
IF TS AND TX>=0AND TY>=44AND TX<=124AND TY<=60GOTO@TOS
IF TS AND TX>=128AND TY>=44AND TX<=255AND TY<=60GOTO@SCOC
IF TS AND TX>=0AND TY>=68AND TX<=124AND TY<=92THEN ACLS:GOTO@VOM
IF TS AND TX>=65AND TY>=130AND TX<=200AND TY<=145GOTO@MENU
IF TS AND TX>=130AND TY>=75AND TX<=250AND TY<=90GOTO@SKIN
IF TS AND TX>=1AND TY>=172AND TX<=254AND TY<=187GOTO@LANGSEL
GOTO@TBTEST
@TBLC
GOSUB@TPCS
GOTO@TBC
@MSC
IF MS==16THEN MS=32ELSE IF MS==32THEN MS=64ELSE IF MS==64THEN MS=128ELSE MS=16
GOTO@TBC
@SOC
IF MOB_ENABLED THEN MOB_ENABLED=0:SSS$="Off"ELSE MOB_ENABLED=1:SSS$="On"
IF MOB_ENABLED THEN MOB_COUNT=1
IF !MOB_ENABLED THEN MOB_COUNT=0
GOTO@TBC
@MOC
IF MOD_ENABLED THEN MOD_ENABLED=0:MSS$="Off"ELSE MOD_ENABLED=1:MSS$="On"
GOTO@TBC
@TOS
IF TSS$=="On"THEN TSS$="Off"ELSE TSS$="On"
GOTO@TBC
@SCOC
MOB_COUNT=MOB_COUNT+1
IF MOB_COUNT==6THEN MOB_COUNT=1
IF !MOB_ENABLED THEN MOB_COUNT=0
GOTO@TBC
@LANGSEL
ACLS
GOSUB@DIRTBG
BX1=75:BY1=50:BX2=175:BY2=65:GOSUB@BTN
BX1=75:BY1=70:BX2=175:BY2=85:GOSUB@BTN
BX1=75:BY1=90:BX2=175:BY2=105:GOSUB@BTN
BX1=75:BY1=110:BX2=175:BY2=125:GOSUB@BTN
PNLSTR 12,7,"English"
PNLSTR 12,9,"Deutsch"
PNLSTR 11,12,"Franc,ais"
PNLSTR 10,14,"(Japanese)"
@LANGSELL
IF TCHST AND TCHX>=75AND TCHY>=50AND TCHX<=175AND TCHY<=65THEN LANG=0:GOTO@TBC
IF TCHST AND TCHX>=75AND TCHY>=70AND TCHX<=175AND TCHY<=85THEN LANG=1:GOTO@TBC
IF TCHST AND TCHX>=75AND TCHY>=90AND TCHX<=175AND TCHY<=105THEN LANG=2:GOTO@TBC
IF TCHST AND TCHX>=75AND TCHY>=110AND TCHX<=175AND TCHY<=125THEN LANG=3:GOTO@TBC
GOTO@LANGSELL

@DIRTBG
GPAGE 1:FOR J=0TO 15:FOR I=0TO 15:GPSET I,J,TEX(I+(16*4),J):NEXT:NEXT:FOR J=0TO 12
FOR I=0TO 16
GCOPY 1,0,0,15,15,I*16,J*16,1:NEXT:NEXT:RETURN

@CRPCONFIG
CLS:GCLS:WAIT 10:GOSUB@DIRTBG
BX1=50:BY1=120:BX2=206:BY2=150:GOSUB@BTN
BX1=50:BY1=20:BX2=206:BY2=65:GOSUB@BTN
BX1=50:BY1=70:BX2=89:BY2=90:GOSUB@BTN
BX1=89:BY1=70:BX2=128:BY2=90:GOSUB@BTN
BX1=128:BY1=70:BX2=167:BY2=90:GOSUB@BTN
BX1=167:BY1=70:BX2=206:BY2=90:GOSUB@BTN

FOR I=0TO 2:I=0:TX=TCHX:TY=TCHY:TT=TCHTIME
IF TT==1AND TX>=50AND TX<=206AND TY>=120AND TY<=150THEN I=2
VSYNC 1:NEXT:GPAGE 0:GOTO@TBC

@BTN
GFILL BX1,BY1,BX2,BY2,1
GFILL BX1,BY1,BX2,BY1+2,252
GFILL BX1,BY1,BX1+2,BY2,252
GFILL BX2-3,BY1+3,BX2,BY2,255
GFILL BX1+3,BY2-3,BX2,BY2,255
GBOX BX1,BY1,BX2,BY2,31
RETURN

@SKIN
HAND1$="MCHAND1
HAND2$="MCHAND2
INPUT "Skin file 1";HAND1$
INPUT "Skin file 2";HAND2$
INPUT "Pallete";HANDY
HAND1$="SPU0:"+HAND1$
HAND2$="BGU0:"+HAND2$
GOTO @DOOPY
@VOM
GPAGE 1:GOSUB@DIRTBG
LOCATE 0,9
PRINT "  –   – ••‘•• ˜••™  ˜••••  ˜•™ "
PRINT "  –   –   –   –  š™ –     ˜› š™"
PRINT "  š™ ˜›   –   –   – ’•••  –   –"
PRINT "   – –    –   –   – –     –   –"
PRINT "   – –    –   –  ˜› –     š™ ˜›"
PRINT "   š•›  •••• š••›  š••••  š•› "
IF LANG==0THEN PNLSTR 0,1," Raytracing      RenderDist     "
IF LANG==1THEN PNLSTR 0,1," Raytracing      SehenFuer      "
IF LANG==0THEN PNLSTR 0,4,"  Screen Width    Bobbing       "
IF LANG==1THEN PNLSTR 0,4," Bildschrmbrte    Wippen        "
IF LANG==0THEN PNLSTR 8,16,"      Back       "
IF LANG==1THEN PNLSTR 8,16,"   Scleissen     "
PNLSTR 12,1,RSS$
PNLSTR 28,1,RDIST$
PNLSTR ABS(FLOOR(SWV/9))+1,4,"–"
PNLSTR 27,4,BOB$
BX1=1:BY1=1:BX2=128:BY2=20:GOSUB@BTN
BX1=130:BY1=1:BX2=254:BY2=20:GOSUB@BTN
BX1=1:BY1=22:BX2=128:BY2=45:GOSUB@BTN
BX1=130:BY1=22:BX2=254:BY2=45:GOSUB@BTN
BX1=65:BY1=120:BX2=200:BY2=145:GOSUB@BTN
GPAGE 0
WAIT 10
@VOMTEST
TX=TCHX
TY=TCHY
TS=TCHST
IF TS AND TX>0AND TY>0AND TX<128AND TY<20GOTO@RTL
IF TS AND TX>129AND TY>0AND TX<255AND TY<20GOTO@RDL
IF TS AND TX>0AND TY>20AND TX<=128AND TY<44GOTO@DWL
IF TS AND TX>129AND TY>20AND TX<255AND TY<44GOTO@BCL
IF TX>=65AND TY>=130AND TX<=200AND TY<=145THEN WAIT 20:GOTO@TBC
GOTO@VOMTEST
@RDL
RDIST$=STR$(VAL(RDIST$)+8)
IF VAL(RDIST$)>32THEN RDIST$="Off"
RDIST=VAL(RDIST$):IF RDIST==0THEN RDIST=999
GOTO@VOM
@RTL
IF RSS$=="Off"THEN RSS$="On"ELSE RSS$="Off"
GOTO@VOM
@DWL
SWV=TCHX
GOTO@VOM
@BCL
IF BOB$=="Off"THEN BOB$="On"ELSE BOB$="Off"
GOTO@VOM

@SSETUP
GOSUB@LOADMAP
MENU=0
GOSUB@TEXT
Q=9
GOTO@LOOP1

@TPCS
TPCHN$="MCDS"
TEXTURES=0
PRINT "Texture Pack?"
PRINT ""
LINPUT TPCHN$
TPCHN$="GRP:"+TPCHN$
GOTO @DOOPY

@CREDITS
ACLS
RESTORE @CREDITDATA
FOR I=0TO CREDITLINES-1
READ CREDITS$(I)
NEXT
FOR I=0TO (CREDITLINES+24)*9
GCOPY 0,1,255,191,0,0,0
GLINE 0,191,255,191,14
J=FLOOR(I/9)
IF !(J>=0AND J<CREDITLINES)GOTO@SKIPPRINT
PRINTLINE$=CREDITS$(J)
PRINTY=191-(I%9)
GOSUB@PRINTLINE
@SKIPPRINT
VSYNC 8
NEXT
GOTO@MENU
@PRINTLINE
LINESIZE=LEN(PRINTLINE$)
PRINTX=FLOOR((256-8*LINESIZE)/2)
FOR PLI=0TO LINESIZE-1
GPUTCHR PRINTX+8*PLI,PRINTY,"BGF",ASC(MID$(PRINTLINE$,PLI,1)),15,1
NEXT
RETURN
@CREDITDATA
DATA "Minecraft DS Credits"
DATA " "
DATA " "
DATA "Main Developer....IAmAPerson"
DATA "Co-Developer....LeviCelJir"
DATA " "
DATA "Raycasting Engine...Calc84maniac"
DATA "Raytracing Engine...Calc84maniac"
DATA "Music....Damolii"
DATA "Music....Randomous"
DATA "Programming Help....Randomous"
DATA "Terrain Generator....Bluerobin"
DATA "Menus....TwinArmageddonz"
DATA "Menus....LeviCelJir"
DATA "Assistance....BonChoi"
DATA "Assistance....Ralph"
DATA " "
DATA "English Translation..IAmAPersson"
DATA "German Translation...IAmAPersson"
DATA "French Translation....PokeYoshi"
DATA "Japanese Translation....Triforce"
DATA " "
DATA " "
DATA "Producers of this update"
DATA "IAmAPerson"
DATA "Triforce"
DATA "TwinArmageddonz"
DATA "PokeYoshi"
DATA "LeviCelJir"
DATA " "
DATA "And to everyone on the"
DATA "NintendoLife Forums and PTC Wiki"
DATA " "
DATA "Thank you..."

@INVENTORY
PNLSTR 0,0,"˜••‘••‘••‘••‘••‘••‘••‘••™"
PNLSTR 0,1,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,2,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,3,"’••“••“••“••“••“••“••“••”"
PNLSTR 0,4,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,5,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,6,"’••“••“••“••“••“••“••“••”"
PNLSTR 0,7,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,8,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,9,"’••“••“••“••“••“••“••“••”"
PNLSTR 0,10,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,11,"–  –  –  –  –  –  –  –  –"
PNLSTR 0,12,"š••••••••••••••••›"
GPAGE 1,1,1
FOR I=0TO 15
FOR J=0TO 15
FOR K=0TO 7
FOR L=0TO 3
GPSET K*24+I+8,J+L*24+8,TEX(K*16+I+L*128,J)
NEXT
NEXT
GPSET 152+I,56+J,TEX(I+512,J)
GPSET 56+I,32+J,TEX(I+544,J)
NEXT
NEXT
WAIT 10

@TINVL
TX=TCHX:TY=TCHY
IF TCHST AND TX>=0AND TY>=0AND TX<=200AND TY<=100GOTO@IFB
GOTO@TINVL

@IFB
'24 per cell
B(IIH-1)=FLOOR(TX/24)+FLOOR(TY/24)*8+1
IF B(IIH-1)==23THEN B(IIH-1)=33
IF B(IIH-1)==11THEN B(IIH-1)=35

@AINVL
INVST=1
ACLS
GOSUB@RESUME
GOSUB@INVBSU
GOSUB@TGUIS
GOSUB@TGUI
BLOCK=B(IIH-1)
GOTO@LOOP1

@INVV
CLS
IF LANG==0THEN PRINT "This map is in another version.":PRINT "A conversion is possible, but"
IF LANG==0THEN PRINT "corruption is a risk. Do you":PRINT "wish to attempt a conversion"
IF LANG==0THEN PRINT "anyway?":PRINT "":PRINT "Yes!   ":PRINT "No! D:"
IF LANG==1THEN PRINT "Diese Karte in einer anderen":PRINT"Version ist!Eine Umwandlung ist"
IF LANG==1THEN PRINT "moeglich aber die Korruption":PRINT "ist ein Risiko. Moechtem Sie"
IF LANG==1THEN PRINT "Umwandlung sowieso versuchen wollen?":?"Ja!    ":?"Nein D:"
INVVC=0
@INVVCHLP
LOCATE 7,6+INVVC:PRINT ""
IF BTRIG()AND 1THEN IF INVVC THEN INVVC=0:LOCATE 7,7:PRINT " "
IF BTRIG()AND 2THEN IF !INVVC THEN INVVC=1:LOCATE 7,6:PRINT " "
IF BUTTON()AND 16GOTO@INVVCC
VSYNC 1
GOTO@INVVCHLP
@INVVCC
IF INVVC THEN RETURN
FOR INVVCL=0TO LEN(VERS$)-1
GPSET 132+INVVCL,0,ASC(MID$(VERS$,INVVCL,1))
NEXT
SYSBEEP=0
UTS$="GRP:"+NAME$
SAVE UTS$
SYSBEEP=1
RETURN

@SLIME
FOR I=0TO MOB_COUNT-1
MOB_NX=MOB_X(I)
MOB_NY=MOB_Y(I)
MOB_LAYER(I)=!MOB_LAYER(I)
IF RND(4)==3THEN MOB_DIRECTION(I)=RND(4)
IF MOB_DIRECTION(I)==0THEN MOB_NX=MOB_X(I)-1
IF MOB_DIRECTION(I)==1THEN MOB_NX=MOB_X(I)+1
IF MOB_DIRECTION(I)==2THEN MOB_NY=MOB_Y(I)-1
IF MOB_DIRECTION(I)==3THEN MOB_NY=MOB_Y(I)+1
IF FLOOR(CAMX+.5)==MOB_NX AND FLOOR(CAMY+.5)==MOB_NY THEN MOB_LAYER(I)=!MOB_LAYER(I):GOTO@SSKIPL
IF MAP(MOB_NX,MOB_NY)OR MAPB(MOB_NX,MOB_NY)THEN MOB_LAYER(I)=!MOB_LAYER(I):GOTO@SSKIPL
IF MOB_LAYER(I)THEN MAP(MOB_X(I),MOB_Y(I))=0
IF !MOB_LAYER(I) THEN MAPB(MOB_X(I),MOB_Y(I))=0
MOB_X(I)=MOB_NX
MOB_Y(I)=MOB_NY
IF !MOB_LAYER(I)AND !INNETHER THEN MAP(MOB_X(I),MOB_Y(I))=11
IF MOB_LAYER(I)AND !INNETHER THEN MAPB(MOB_X(I),MOB_Y(I))=11
IF !MOB_LAYER(I)AND INNETHER THEN MAP(MOB_X(I),MOB_Y(I))=34
IF MOB_LAYER(I)AND INNETHER THEN MAPB(MOB_X(I),MOB_Y(I))=34
@SSKIPL
NEXT
RETURN

@BLOCKBREAK
'---Tells if you can break a
'---block or not
DATA 0
DATA 1,1,1,1,1,1,1,1,1,0,0,1,1,1,1,1
DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
DATA 1,0,1

@WETHANDS
DATA "{MAIN=L8Q8O2A<C+A(B(<C+(>B(A(E(D(F+(<C+(E(C+(}"
DATA "{MAINLOW=L8Q8O2GB<DF+AF+D>BGB<DF+A2}"
DATA "{FMT=R2}"
DATA ":0@0T72V65"
DATA "@E115,10,0,100{MAIN}>A4.V80{MAIN}>A4.V95{MAIN}>A4.V95{MAIN}>A8R4V95"
DATA "[{MAINLOW}]2O2A<C+AB<C+>BAE>A<V105C+V115EBR2{FMT}V95[O2B<DF+A<C+2]2"
DATA "O2GB<DGA2V70E1V80O2EG+B<EG+E>BG+EG+B<EG+E>A4V70GB<DF+AF+D>BA<C+EA<C+>BAE"
DATA "[O2EG+B<EG+2]2V50O2@E110,5,0,55E1E1"
DATA ":1@0T72"
DATA "[R1]4Q8@E120,30,0,55
DATA "G+2.AF+2.E8F+8G+2.B8<C+4>F+8&F+2<C+8E8
DATA "G4.F+8D>A8B8&B1<GF+D>A8B8&B2.AER2.R2O4C+8E8A8<C+8&C+2"
DATA "D4.C+8>V100AV127<E8F+8R8D8&D2>B8<C+8DC+8DF+4.V90C+2.>V70BO3@E110,5,0,55E1V85B1
DATA "@E120,30,0,55V80O5L8GF+EDEDEF+RE4.A2G+E>BG+E2BG+E>BV40G+2
DATA "V50[O3EG+B<E>BG+4.]2
DATA ":2@0T72V50"
DATA "[R1]12R4Q8@E120,30,0,55
DATA "O3AR2R1{FMT}V100O4B4.R8V45F+4V100E8F+8R1R1V80E2.O3V100B8A8V70@E110,5,0,55G+1R1"
DATA "R1R2@E120,30,0,55O5V50E2
DATA ":3@0T72V100"
DATA "[R1]14Q8@E120,30,0,55{FMT}"
DATA "O4F+4.R8R2R1R1O2V70A1O3V60@E110,5,0,55E1R1"
DATA 0

@TERRGEN
NAMEY$="1"
GOSUB@VARS
GOSUB@GETSEED
S1=MAINCNTL
RDIV=7:GOSUB@RAND
FOR Z=0TO 10+RAND
RDIV=MS-1:GOSUB@RAND
X=RAND
RDIV=MS-1:GOSUB@RAND
Y=RAND
TGEN(0)=0+X
TGEN(1)=0+Y
TGEN(2)=ABS(MS-1-X)
TGEN(3)=ABS(MS-1-Y)
SORT 0,4,TGEN
RDIV=TGEN(0)+1:GOSUB@RAND
IF !GN THEN GCIRCLE X,Y,RAND-4,13
IF GN THEN GCIRCLE X,Y,RAND-4,35
NEXT
GPAINT 0,0,6
FOR X=0TO 100
FOR Y=0TO 100
IF !GN AND !GSPOIT(X,Y)THEN GPAINT X,Y,12
IF GN AND !GSPOIT(X,Y)THEN GPAINT X,Y,35
NEXT
NEXT
GPAINT 0,0,0
GCOPY 0,0,63,63,64,0,TRUE
FOR X=0TO MS-1
FOR Y=0TO MS-1
IF !GN AND GSPOIT(X,Y)==13THEN GPSET X,Y,12
IF !GN AND GSPOIT(X,Y)==12THEN GPSET X,Y+64,13
IF GN AND GSPOIT(X,Y)==35THEN GPSET X,Y+64,35
NEXT
NEXT
GBOX 0,0,MS-1,MS-1,10
GBOX 0,64,MS-1,64+MS-1,10
RETURN
@VARS
B=FALSE
BX=0
BY=0
IRAND=0
RAND=0
SEED=0
IF SEED$=="" THEN SEED$=CHR$(RND(200))+CHR$(RND(200))+CHR$(RND(200))+CHR$(RND(200))+CHR$(RND(200))
RDIV=1
RETURN
@GETSEED
FOR Z=0TO LEN(SEED$)
SEED=SEED+ASC(LEFT$(RIGHT$(SEED$,1+Z),1))
NEXT
IRAND=SEED
RETURN
@RAND
IRAND=(IRAND*7+4444)%59049
RAND=IRAND%RDIV
RETURN

@TNT
FOR S=0TO 3
BEEP 197
WAIT 2
NEXT
FOR U=-2TO 2
FOR R=-2TO 2
IF BREAKABLE(MAP(I+R,J+U))THEN MAP(I+R,J+U)=0:MAPB(I+R,J+U)=0
NEXT
NEXT
RETURN

@DRAWGROUND
C=COS(CA)
S=SIN(CA)
FOR J=96TO 191STEP FLOOR(Q/2)+1
K=J+FLOOR(Q/2)
Z=50/(J-94)
I=Z/55
M=I*S
N=-I*C
X=CX+Z*C-M*32
Y=CY+Z*S-N*32
FOR F=ABS(SWV-128)TO SWV+132STEP 4
X=X+M
Y=Y+N
IF!(X OR Y AND-64)THEN GFILL F,J,F+3,K,T(M(X,Y)+Y%1*16,X%1*16)
NEXT
NEXT
RETURN

@INITGFX
FOR T=0TO 511STEP 16
FOR J=0TO 15
FOR I=0TO 15
T(T+J,I)=TEX(J+176+(INNETHER*368),I)
NEXT
NEXT
NEXT
RETURN

@MAKEAP
FOR I=4TO MS-4
FOR J=4TO MS-4
IF MAP(I,J)==0AND MAP(I,J-2)==0THEN UTX=I:UTY=J:I=256:J=192
NEXT
NEXT
MAP(UTX,UTY)=28
MAPB(UTX,UTY)=28
MAP(UTX,UTY-2)=28
MAPB(UTX,UTY-2)=28
MAP(UTX,UTY-1)=18
MAPB(UTX,UTY-1)=18
IF MAP(UTX+1,UTY-1)>0THEN STOREDX=UTX-2:STOREDY=UTY-1ELSE STOREDX=UTX+1:STOREDY=UTY-2
RETURN

@NETHERLOAD
ACLS
LOCATE 7,11
?"Loading Dimension"
FOR I=0TO 127
FOR J=0TO 127
IF MAP(I,J)!=11AND MAP(I,J)!=34THEN SWAP MAP(I,J),NETHER(I,J)ELSE MAP(I,J)=0
IF MAPB(I,J)!=11AND MAPB(I,J)!=34THEN SWAP MAPB(I,J),NETHERB(I,J)ELSE MAPB(I,J)=0
IF MAP(I,J)==18AND !INNETHER THEN HASP=1
NEXT
NEXT
IF !HASP AND !INNETHER THEN GOSUB@MAKEAP
SWAP CAMX,STOREDX
SWAP CAMY,STOREDY
X=CAMX
Y=CAMY
SYSBEEP=0
LOAD"GRP0:MCDS_BG",0
SYSBEEP=1
INNETHER=!INNETHER
CLS
IF INNETHER THEN GPAGE 0,0,0:SYSBEEP=0:LOAD"GRP:NETHER",0:SYSBEEP=1
GOSUB@TGUIS
GOSUB@INITGFX
RETURN

'-_-ø-_-ø-_-ø-_-ø-_-        
'                           
'Place mods here!           
'                           
'-_-ø-_-ø-_-ø-_-ø-_-        

@MODS

RETURN
